use crate::ast::*;

// lalrpop 里的约定
grammar;

// 约束 lexer 的行为
match {
  // 跳过空白符和注释
  r"\s*" => {},
  r"//[^\n\r]*[\n\r]*" => {},
  //r"/\*([^*]|\*[^/])*\*/" => {}, // 无法处理 /* **/
  r"/\*([^*]|(\*+([^*/])))*\*+/" => {},
  // 剩下的情况采用默认方式处理
  _
}

// 定义 CompUnit, 其返回值类型为 String
// parser 在解析完成后的行为是返回 FuncDef 的值
//pub CompUnit: String = <func_def: FuncDef> => func_def;
pub CompUnit: CompUnit = <func_def: FuncDef> => CompUnit { <> };

FuncDef: FuncDef = {
  <func_type: FuncType> <ident: Ident> "(" ")" <block: Block> => {
    FuncDef { <> }
  }
}

FuncType: FuncType = {
    "int" => FuncType::Int,
};


//Block         ::= "{" {BlockItem} "}";
Block: Block = "{" <items: (BlockItem)*> "}" => Block { <> };

//BlockItem     ::= Decl | Stmt;
BlockItem: BlockItem = {
    <decl: Decl> => BlockItem::Decl(decl),
    <stmt: Stmt> => BlockItem::Stmt(stmt),
};


//Stmt ::= MatchedStmt
//       | OpenStmt
Stmt: Stmt = {
    <matched_stmt: MatchedStmt> => matched_stmt,
    <open_stmt: OpenStmt> => open_stmt,
};

//MatchedStmt ::= LVal "=" Exp ";"
//              | [Exp] ";"
//              | Block
//              | "if" "(" Exp ")" MatchedStmt "else" MatchedStmt
//              | "return" [Exp] ";";
MatchedStmt: Stmt = {
    <lval: LVal> "=" <exp: Exp> ";" => Stmt::Assign(lval, exp),
    <exp: (Exp)?> ";" => Stmt::Exp(<>),
    Block => Stmt::Block(<>),
    "if" "(" <exp: Exp> ")" <then_stmt: MatchedStmt> "else" <else_stmt: MatchedStmt>
       => Stmt::If(exp, Box::new(then_stmt), Some(Box::new(else_stmt))),
    "return" <exp: (Exp)?> ";" => Stmt::RetExp(<>),
};

//OpenStmt ::= "if" "(" Exp ")" MatchedStmt ["else" OpenStmt]
OpenStmt: Stmt = {
    //见https://yuhanawa.github.io/posts/2023/57877/#6-%E5%AE%8F-Macros
    //注意到else是Option<Stmt>类型,需要用map转为Option<Box<Stmt>>
    "if" "(" <exp: Exp> ")" <stmt: MatchedStmt> <some_else: ("else" <OpenStmt>)?> => 
      Stmt::If(exp, Box::new(stmt), some_else.map(|(x)| Box::new(x))),
};


//LVal          ::= IDENT;
LVal: LVal = {
    <ident: Ident> => LVal{<>},
};

Number: i32 = {
    <num: IntConst> => <>,   
};


// 也是IDENT
Ident: String = {
    r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string(),   
};

// 对整数字面量的处理方式: 把匹配到的字符串按对应进制转换成数字
IntConst: i32 = {
  r"[1-9][0-9]*" => i32::from_str_radix(<>, 10).unwrap(),
  r"0[0-7]*" => i32::from_str_radix(<>, 8).unwrap(),
  r"0[xX][0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
}

//Exp         ::= LOrExp;
Exp: Exp = {
  <lor_exp : LOrExp> => {
    Exp::LOrExp(Box::new(lor_exp))
  }
};

//UnaryOp     ::= "+" | "-" | "!";
UnaryOp: UnaryOp = {
  "-" => UnaryOp::Neg,
  "+" => UnaryOp::Pos,
  "!" => UnaryOp::Not,
}

//BinaryAddOp    ::= "+" | "-" ;
BinaryAddOp: BinaryAddOp = {
  "+" => BinaryAddOp::Add,
  "-" => BinaryAddOp::Sub,
}

//BinaryMulOP    ::= "*" | "/" | "%" ;
BinaryMulOp: BinaryMulOp = {
  "*" => BinaryMulOp::Mul,
  "/" => BinaryMulOp::Div,
  "%" => BinaryMulOp::Mod,
}

//UnaryExp    ::= PrimaryExp | UnaryOp UnaryExp;
UnaryExp: UnaryExp = {
  <primary_exp: PrimaryExp> => UnaryExp::PrimaryExp(Box::new(primary_exp)),
  <unary_op: UnaryOp> <unary_exp: UnaryExp> => UnaryExp::BinaryOp(unary_op, Box::new(unary_exp)),
}

//PrimaryExp    ::= "(" Exp ")" | LVal | Number;
PrimaryExp: PrimaryExp = {
  "(" <exp: Exp> ")" => PrimaryExp::Bexp(Box::new(exp)),
  <lval: LVal> => PrimaryExp::LVal(lval),
  <num: Number> => PrimaryExp::Number(<>),
}

//MulExp      ::= UnaryExp | MulExp ("*" | "/" | "%") UnaryExp;
MulExp: MulExp = {
  <unary_exp: UnaryExp> => MulExp::UnaryExp(Box::new(unary_exp)),
  <mul_exp: MulExp> <op: BinaryMulOp> <unary_exp: UnaryExp> => MulExp::BinaryExp(Box::new(mul_exp), op, Box::new(unary_exp)),
}

//AddExp      ::= MulExp | AddExp ("+" | "-") MulExp;
AddExp: AddExp = {
  <mul_exp: MulExp> => AddExp::MulExp(Box::new(mul_exp)),
  <add_exp: AddExp> <op: BinaryAddOp> <mul_exp: MulExp> => AddExp::BinaryExp(Box::new(add_exp), op, Box::new(mul_exp)),
}

//BinaryRelOp ::= "<" | ">" | "<=" | ">=" ;
BinaryRelOp: BinaryRelOp = {
  "<" => BinaryRelOp::Lt,
  ">" => BinaryRelOp::Gt,
  "<=" => BinaryRelOp::Le,
  ">=" => BinaryRelOp::Ge,
}

//RelExp      ::= AddExp | RelExp ("<" | ">" | "<=" | ">=") AddExp;
RelExp: RelExp = {
  <add_exp: AddExp> => RelExp::AddExp(Box::new(add_exp)),
  <rel_exp: RelExp> <op: BinaryRelOp> <add_exp: AddExp> => RelExp::BinaryExp(Box::new(rel_exp), op, Box::new(add_exp)),
}

//BinaryEqOp  ::= "==" | "!=" ;
BinaryEqOp: BinaryEqOp = {
  "==" => BinaryEqOp::Eq,
  "!=" => BinaryEqOp::Ne,
}

//EqExp       ::= RelExp | EqExp ("==" | "!=") RelExp;
EqExp: EqExp = {
  <rel_exp: RelExp> => EqExp::RelExp(Box::new(rel_exp)),
  <eq_exp: EqExp> <op: BinaryEqOp> <rel_exp: RelExp> => EqExp::BinaryExp(Box::new(eq_exp), op, Box::new(rel_exp)),
}

//LAndExp     ::= EqExp | LAndExp "&&" EqExp;
LAndExp: LAndExp = {
  <eq_exp: EqExp> => LAndExp::EqExp(Box::new(eq_exp)),
  <land_exp: LAndExp> "&&" <eq_exp: EqExp> => LAndExp::BinaryExp(Box::new(land_exp), Box::new(eq_exp)),
}

//LOrExp      ::= LAndExp | LOrExp "||" LAndExp;
LOrExp: LOrExp = {
  <land_exp: LAndExp> => LOrExp::LAndExp(Box::new(land_exp)),
  <lor_exp: LOrExp> "||" <land_exp: LAndExp> => LOrExp::BinaryExp(Box::new(lor_exp), Box::new(land_exp)),
}



//Decl          ::= ConstDecl | VarDecl;
Decl: Decl = {
  <const_decl: ConstDecl> => Decl::ConstDecl(const_decl),
  <var_decl: VarDecl> => Decl::VarDecl(var_decl),
}


//ConstDecl     ::= "const" BType ConstDef {"," ConstDef} ";";
ConstDecl: ConstDecl = {
  "const" <btype: BType> <constdef_s: Comma<ConstDef> > ";" => {
    ConstDecl::ConstDeclS(btype, constdef_s)
  }
} 

//Comma宏
//见https://yuhanawa.github.io/posts/2023/57877/#6-%E5%AE%8F-Macros
Comma<T>: Vec<T> = {
  <mut v:(<T> ",")*> <e:T?> => match e {
    None => v,
    Some(e) => {
        v.push(e);
        v
    }
  }
}

//BType         ::= "int";
BType: BType = {
  "int" => BType::Int,
}

//ConstDef      ::= IDENT "=" ConstInitVal;
ConstDef: ConstDef = {
  <ident: Ident> "=" <const_init_val: ConstInitVal> => {
    ConstDef{<>}
  }
}

//ConstInitVal  ::= ConstExp;
ConstInitVal: ConstInitVal = {
  <const_exp: ConstExp> => ConstInitVal::ConstExp(const_exp)
}

//ConstExp      ::= Exp;
ConstExp: ConstExp = {
  <exp: Exp> => ConstExp::Exp(exp)
}

//VarDecl       ::= BType VarDef {"," VarDef} ";";
VarDecl: VarDecl = {
  <btype: BType> <vardef_s: Comma<VarDef> > ";" => {
    VarDecl::VarDeclS(btype, vardef_s)
  }
}

//VarDef        ::= IDENT | IDENT "=" InitVal;
VarDef: VarDef = {
  <ident: Ident> "=" <init_val: InitVal> => {
    VarDef::Init(ident, init_val)
  },
  <ident: Ident> => {
    VarDef::NoInit(ident)
  }
}

//InitVal       ::= Exp;
InitVal: InitVal = {
  <exp: Exp> => InitVal::Exp(exp)
}

